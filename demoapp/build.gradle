apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.monits.staticCodeAnalysis'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: rootProject.file('jacoco.gradle')
apply from: rootProject.file('detekt.gradle')

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:$dexCountGradlePlugin"
    }
}

android {
    compileSdkVersion maxApiLevel.toInteger()
    buildToolsVersion androidBuildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "$libraryGroupId" + ".demoapp"
        minSdkVersion minApiLevel
        targetSdkVersion maxApiLevel
        versionCode "$demoAppVersionCode".toInteger()
        versionName "$libraryVersion"

        multiDexEnabled true

        resValue("string", "andesui_demoapp_app_name", "Andes v${libraryVersion}")
    }

    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug'
            minifyEnabled false
            aaptOptions {
                cruncherEnabled false
            }

            resValue("string", "andesui_demoapp_app_name", "Andes DEBUG")
        }
        release {
            initWith(buildTypes.debug)
            // We need this in true to test after executing ProGuard to check our ProGuard custom rules.
            minifyEnabled true
            shrinkResources true
        }
    }

    lintOptions {
        // TODO: Delete this once play services is updated. Now we need it because play services brings support library 26
        disable 'GradleCompatible'
        disable 'UnusedResources'
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
    }

    dexOptions.preDexLibraries = true
}

staticCodeAnalysis {
    // False is REQUIRED in order to make CI server fail the build on errors.
    ignoreErrors = false

    androidLintConfig = "$project.rootProject.projectDir/config/android/android-lint-demoapp.xml"
}

dependencies {
    implementation project(':components')
    implementation prod.kotlinStdlib
    implementation prod.supportConstraintLayout
    implementation prod.supportLibraryAppCompatV7
    implementation prod.fresco
    implementation "com.github.bumptech.glide:glide:4.3.1"

    /**
     * It enables us to show the What's New which is in markdown format.
     * https://github.com/noties/Markwon
     */
    implementation 'io.noties.markwon:core:4.5.1'
//    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}

/**
 * Based on https://stackoverflow.com/a/44218054/2823516
 */
copy {
    from file ('../CHANGELOG.md')
    into 'src/main/res/raw'
    rename { String fileName ->
        fileName.replace("CHANGELOG.md", "andesui_demoapp_changelog.md")
    }
}